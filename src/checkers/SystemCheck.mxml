<?xml version="1.0" encoding="utf-8"?>
<mx:VBox xmlns:mx="http://www.adobe.com/2006/mxml" width="400" height="300" >
	<mx:Script>
		<![CDATA[
			import connectors.AppsConnector;
			
			import mx.core.Application;
			import mx.managers.PopUpManager;
			
			import util.JavaCheck;
			import util.Requirements;
						
			public function init():void{
				checkFlash();
				checkOS();
				checkJava();
			}
			
			private function checkFlash():void{
				var flashVersion:String = Capabilities.version;
				
				var flashVersions:Array = (Capabilities.version.split(" ")[1] as String).split(",");
				var flashRequired:Array = Requirements.flash_required_version.split(",");
				
				trace("version is: " + flashVersions);
				trace("required is " + flashRequired);
				
				for (var i:Number = 0; i< flashRequired.length; i++){
					if (Number(flashRequired[i]) > Number(flashVersions[i])) showFlashError(flashVersion, Requirements.flash_required_version);
				}
				
			}
			
			private function checkOS():void{
				trace("Operating System: " + Capabilities.os);
				var os:Array = Capabilities.os.split(" ");
				//Check for this bug: http://reviews.cnet.com/8301-13727_7-20022842-263.html
				//Fix is to download new flash player 10.2 or above
				if (os[0] == "Mac" && os[1] == "OS" && os[2] == "10.6.5"){
					showMacOS1065iSightError();
				}
			}
			
			private function checkJava():void{
				var javas:Array = JavaCheck.getJREs();
				
				if (javas.length == 0) showFlashError("NO JAVA INSTALLED", Requirements.java_required_version);
				
				trace("Java version required is: " + Requirements.java_required_version);
				trace("Java versions installed: ");
				var highestJava:String = javas[0];
				for each (var java:String in javas){
					trace("Found java " + java);
					var highest:Array = highestJava.split(".");
					var iter:Array = java.split(".");
					
					if (Number(iter[0]) > Number(highest[0])){
						highestJava = java;
					} else if (Number(iter[0]) == Number(highest[0]) && Number(iter[1]) > Number(highest[1])){
						highestJava = java;
					} else if (Number(iter[0]) == Number(highest[0]) && Number(iter[1]) == Number(highest[1])){
						var iterMinor:Number = Number((iter[2] as String).split("_")[1]);
						var highestMinor:Number = Number((highest[2] as String).split("_")[1]);
						if (iterMinor > highestMinor) highestJava = java;
					}
				}
				
				var passedJava:Boolean = true;
				var required:Array = Requirements.java_required_version.split(".");
				highest = highestJava.split(".");
				if (Number(required[0]) > Number(highest[0])){
					passedJava = false;
				} else if (Number(required[0]) == Number(highest[0]) && Number(required[1]) > Number(highest[1])){
					passedJava = false;
				} else if (Number(required[0]) == Number(highest[0]) && Number(required[1]) == Number(highest[1])){
					var requiredMinor:Number = Number((required[2] as String).split("_")[1]);
					var highestJavaMinor:Number = Number((highest[2] as String).split("_")[1]);
					if (requiredMinor > highestJavaMinor) passedJava = false;
				}
				
				if (!passedJava) showJavaError(highestJava, Requirements.java_required_version);
			}
			
			private function checkBBBApps():void{
				var appsConnector:AppsConnector = new AppsConnector();
			}
			
			private function showJavaError(hasVersion:String, needsVersion:String):void{				
				var userAlert:UserAlert = UserAlert(PopUpManager.createPopUp(this.parent, UserAlert, true));
				userAlert.setAlert(javaError(hasVersion, needsVersion));
				
				userAlert.x = Application.application.width/2 - userAlert.width/2;
				userAlert.y = Application.application.height/2 - userAlert.height/2;
			}
			
			private function showFlashError(hasVersion:String, needsVersion:String):void{
				var userAlert:UserAlert = UserAlert(PopUpManager.createPopUp(this.parent, UserAlert, true));
				userAlert.setAlert(flashError(hasVersion, needsVersion));
				
				userAlert.x = Application.application.width/2 - userAlert.width/2;
				userAlert.y = Application.application.height/2 - userAlert.height/2;
			}
			
			private function showMacOS1065iSightError():void{
				var userAlert:UserAlert = UserAlert(PopUpManager.createPopUp(this.parent, UserAlert, true));
				userAlert.setAlert(iSightErrorOSX1065());
				
				userAlert.x = Application.application.width/2 - userAlert.width/2;
				userAlert.y = Application.application.height/2 - userAlert.height/2;
			}
			
			private static function iSightErrorOSX1065():String{
				return "If you have problems with your iSight camera, it may be because you are running OS X 10.6.5, which is known" +
						" to have a problem with Flash displaying the iSight camera. \n\n" + 
						"To correct this, install a newer version of Flash"+
						" from <a href='http://labs.adobe.com/downloads/flashplayer10.html'><font color='#0000ff'>Adobe</font></a>, " + 
						" or update your Mac to the newest version";
			}
			
			private static function javaError(hasVersion:String, needsVersion:String):String{
				return "You have Java " + hasVersion + " installed, but you need at least version " + needsVersion +
						" to use the BigBlueButton desktop sharing feature. Click on the button bellow to install the newest" +
						" Java JRE version.";
			}
			
			private static function flashError(hasVersion:String, needsVersion:String):String{
				return "You have Flash " + hasVersion + " installed, but you need at least version " + needsVersion +
						" to run BigBlueButton properly. Click on the button bellow to install the newest" +
						" Adobe Flash version.";
			}
		]]>
	</mx:Script>
	
	<mx:Text id="txtOutput" width="100%" height="100%" text="" selectable="false"/>
	
</mx:VBox>
